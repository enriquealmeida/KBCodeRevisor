======OBJECT = ColumnsSelector.WebPanel === Columns Selector=====

=== RULES ===
parm(in:&ColumnsSelectorInitialXML, in:&SessionKey);
noaccept(&ColumnName);


=== EVENTS SOURCE ===

Event Start

	if &HttpRequest.Method = HttpMethod.Get and &ColumnsSelectorXML.IsEmpty()
		&ColumnsSelectorSDT.Clear()
		&ColumnsSelector2.FromXml(&ColumnsSelectorInitialXML)
		&i = 1
		For &visibleColumn in &ColumnsSelector2.VisibleColumns
			&ColumnsSelectorSDTItem = new()
			&ColumnsSelectorSDTItem.ColumnName = &VisibleColumn.ColumnName
			&ColumnsSelectorSDTItem.Visible = true
			
			&ColumnsSelectorSDT.Add( &ColumnsSelectorSDTItem,&i)
			&i += 1
		endfor
		For &invisibleColumn in &ColumnsSelector2.invisibleColumns
			&ColumnsSelectorSDTItem = new()
			&ColumnsSelectorSDTItem.ColumnName = &InvisibleColumn.ColumnName
			&ColumnsSelectorSDTItem.Visible = false
			
			&ColumnsSelectorSDT.Add(&ColumnsSelectorSDTItem,&i)
			&i += 1
		endfor
	
		&ColumnsSelectorXML = &ColumnsSelectorSDT.ToXml()
	
	endif

	//Comment these lines if you are using Columns Selector with Free Style Grid
	&MoveDown.Visible = false
	&MoveUp.Visible = false

	/* Generated by DVelop Work With Plus Pattern [Start] - Do not change */

	&ColumnsSelectorXML.Visible = False
	Grid.Rows = 0

	/* Generated by DVelop Work With Plus Pattern [End] - Do not change */

	if 1 = 0
		Grid.NextPage()
	endif

EndEvent

Event Grid.load

	For &ColumnsSelectorSDTItem in &ColumnsSelectorSDT
		&ColumnName = &ColumnsSelectorSDTItem.ColumnName
		&ColumnDisplayName = GetMessageText(&ColumnsSelectorSDTItem.ColumnName)
		&Visible = &ColumnsSelectorSDTItem.Visible

	/* Generated by DVelop Work With Plus Pattern [Start] - Do not change */

	//this code should be inside the for each used to load the grid
	&MoveUp.FromImage(AssociationUp)
	&MoveUp.TooltipText = ""
	
	&MoveDown.FromImage(AssociationDown)
	&MoveDown.TooltipText = ""

	/* Generated by DVelop Work With Plus Pattern [End] - Do not change */

    load
	endfor

EndEvent

Event Refresh

	&ColumnsSelectorSDT.Clear()
	&ColumnsSelectorSDT.FromXml(&ColumnsSelectorXML)

EndEvent

Event Enter

	do 'loadColumnsSelectorSDT'
	&AtLeastAColumnVisible = false
	for &ColumnsSelectorSDTItem in &ColumnsSelectorSDT
		if &ColumnsSelectorSDTItem.Visible = true
			&AtLeastAColumnVisible = true
			exit
		endif
	endfor

	if (&AtLeastAColumnVisible)
		&ColumnsSelector2 = new()
		for &ColumnsSelectorSDTItem in &ColumnsSelectorSDT
			if &ColumnsSelectorSDTItem.Visible = true
				&VisibleColumn = new()
				&VisibleColumn.ColumnName = &ColumnsSelectorSDTItem.ColumnName
				&ColumnsSelector2.VisibleColumns.Add(&VisibleColumn)
			else
				&InvisibleColumn = new()
				&InvisibleColumn.ColumnName = &ColumnsSelectorSDTItem.ColumnName
				&ColumnsSelector2.InvisibleColumns.Add(&InvisibleColumn)
			endif
		endfor
	
		SaveColumnsSelectorState.Call(&SessionKey, &ColumnsSelector2.ToXml())
		&ws.Set(&SessionKey, &ColumnsSelector2.ToXml())
		return
	else
		msg('At least one column must be shown')
	endif

EndEvent

Event &MoveUp.Click

	&SelectedColumnName = &ColumnName
	&SelectedVisible = &Visible
	do 'loadColumnsSelectorSDT'
	&i = 1
	for &ColumnsSelectorSDTItem in &ColumnsSelectorSDT
		if &SelectedColumnName = &ColumnsSelectorSDTItem.ColumnName
			exit
		else
			&i += 1
		endif
	endfor
	if &i > 1
		&ColumnsSelectorSDT.remove(&i)
		
		&ColumnsSelectorSDTItem = new()
		&ColumnsSelectorSDTItem.ColumnName = &SelectedColumnName
		&ColumnsSelectorSDTItem.Visible = &SelectedVisible
		&ColumnsSelectorSDT.Add(&ColumnsSelectorSDTItem, &i -1)
	endif
	&ColumnsSelectorXML = &ColumnsSelectorSDT.ToXml()
  	Grid.Refresh()

EndEvent

Event &MoveDown.Click

	&SelectedColumnName = &ColumnName
	&SelectedVisible = &Visible
	do 'loadColumnsSelectorSDT'
	&i = 1
	for &ColumnsSelectorSDTItem in &ColumnsSelectorSDT
		if &SelectedColumnName = &ColumnsSelectorSDTItem.ColumnName
			exit
		else
			&i += 1
		endif
	endfor
	if &i < &ColumnsSelectorSDT.Count
		&ColumnsSelectorSDT.remove(&i)
		
		&ColumnsSelectorSDTItem = new()
		&ColumnsSelectorSDTItem.ColumnName = &SelectedColumnName
		&ColumnsSelectorSDTItem.Visible = &SelectedVisible
		&ColumnsSelectorSDT.Add(&ColumnsSelectorSDTItem, &i+1)
	endif
	&ColumnsSelectorXML = &ColumnsSelectorSDT.ToXml()
  	Grid.Refresh()

EndEvent

Sub 'loadColumnsSelectorSDT'

	&ColumnsSelectorSDT = new()
	&i = 1
	for each line in Grid
		&ColumnsSelectorSDTItem = new()
		&ColumnsSelectorSDTItem.ColumnName = &ColumnName
		&ColumnsSelectorSDTItem.Visible = &Visible
		
		&ColumnsSelectorSDT.Add( &ColumnsSelectorSDTItem,&i)
		&i += 1
	endfor
	&ColumnsSelectorXML = &ColumnsSelectorSDT.ToXml()

EndSub




====== PROPERTIES =======
Name -> ColumnsSelector
Description -> Columns Selector
Apply:07135890-56fc-489b-b408-063722fa9f7d -> True
FolderType -> c88fffcd-b6f8-0000-8fec-00b5497e2117
FolderId -> 2
ObjectVisibility -> Private
IsDefault -> False
