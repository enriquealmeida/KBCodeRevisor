======OBJECT = HHrsNoIng2.WebPanel === Horas no ingresadas=====

=== RULES ===
parm(IN: &FchIniPar, in: &FchFinPar);


=== EVENTS SOURCE ===

Event Start

    if &FchIniPar.IsEmpty()
	   &FchIni = YMDtoD( Year(Today()), Month(Today()), 01 )
	else 
	   &FchIni = &FchIniPar
	endif
	&vMes = Month(Today())
	&Mes = MesValues.Convert(&vMes)
	&Anio = Year(Today())
	
	if &FchFinPar.IsEmpty()
	   &FchFnl = today()
	else
		&FchFnl = &FchFinPar
	endif

    &vImpRsp = Bool.True 
	&Rojo = rgb(255,0,0)
	&Negro=rgb(0,0,0)
	&verde=rgb(42,98,105)
	&blanco = rgb(255,255,255)
	&Inicializar = true

	/* Generated by DVelop Work With Plus Pattern [Start] - Do not change */



	/* Generated by DVelop Work With Plus Pattern [End] - Do not change */

EndEvent

Event Responsables.load

	Do 'EstiloTituloresponsable'
	
	//Cargo el cabezal
	
	Responsables.load()
	
	Do 'SacarEstiloTituloresponsable'
	
	&ResponsablesActivos = ResponsablesActivosSinChequeoDeAtrasos()
	for &Responsable in &ResponsablesActivos
		&RspId = &Responsable.DRspId
		&RangoLicencias = LicenciaEntreFechas(&RspId,&FchIni,&FchFnl)
		&DiasTrabajados = DiasTrabajadosPorResponsable(&RspId,&FchIni,&FchFnl)
		
		for each 
			where      RspId = &RspId
			&RspHorDia = RspSueHr
		endfor
		
		Responsables.load()
	endfor

EndEvent

Event Dias.load

	Do 'SacarEstiloFeriados'
	&DiaActual = &vDiaIniMes
	
	//Si entro por primera vez, cargo los días del mes en el título. 
	
	if (&Inicializar)
		Do While &DiaActual <= &vDiaFinMes
			&Char = Str(&DiaActual,2,0)
			&DiaActual = &DiaActual + 1 
			Do 'EstiloTituloDias'
			Dias.load()
		EndDo
		&Inicializar = false
		Do 'SacarEstiloTituloDias'
	endif
	
	//Recorro los días y cargo el caracter correspondiente en la grilla
	
	Do While &DiaActual <= &vDiaFinMes and not &Inicializar
		
		&EsFeriado = false
		&Dia.Dias = YMDtoD(&vAnio, &vMes, &DiaActual)
		&DiaNum = dow(&Dia.Dias)
		&Char = '*'
		
		if EsFeriado(&Dia.Dias)="S" or &DiaNum = 1 or &DiaNum = 7 
		    &Char = 'F'
			&EsFeriado = true
        else 
			if EsLicencia(&Dia,&Responsable,&RangoLicencias)
				&Char = TipoLicencia(&Dia.Dias, &RspId)
				&HrsTot = 0
			else
				if PerteneceDia(&Dia,&DiasTrabajados)
					&HrsTot = TotalHorasResponsable(&Dia.Dias,&RspId)
					&Char = Str(&HrsTot, 2 , 0)
				endif
			endif
		endif
		
		//Se le da color y estilo al caracter
		
		if &HrsTot < &RspHorDia and not(&EsFeriado)
			Do 'EstiloFeriados'
		else
			Do 'SacarEstiloFeriados'
		endif
		Dias.load()
		&DiaActual = &DiaActual+1
	enddo

EndEvent

Event Refresh

	 // Inicializo los días
	&FchIni = YMDtoD( &Anio, &Mes.Integer(), 1 )
	&FchFnl = YMDtoD( &Anio, &Mes.Integer(), day(eom(&FchIni)))
    &vDiaFinMes = day(&FchFnl)
    &vDiaIniMes = Day(&FchIni)
    &vMes = month(&FchIni)
    &vAnio = year(&FchIni)
    &DiasMes = day( eom(&FchFnl) )
	Dias.Columns = &DiasMes 
	Responsables.Columns = 1
	&Inicializar = true

EndEvent

Sub 'EstiloTituloresponsable'

	&RspId = 'Responsables'
	&RspId.FontSize = &RspId.FontSize + 1
	Table2.Backcolor = &verde
	&RspId.ForeColor = &blanco

EndSub

Sub 'SacarEstiloTituloresponsable'

	&RspId.ForeColor = &Negro
	Table2.Backcolor = &Negro
	&RspId.FontSize = &RspId.FontSize - 1

EndSub

Sub 'EstiloTituloDias'

	&Char.FontSize = &RspId.FontSize + 1
	&Char.ForeColor = &blanco

EndSub

Sub 'SacarEstiloTituloDias'

	&Char.FontSize = &RspId.FontSize - 1
	&Char.ForeColor =  &Negro

EndSub

Sub 'EstiloFeriados'

	&Char.ForeColor = &rojo
	&Char.FontBold = 1

EndSub

Sub 'SacarEstiloFeriados'

	&Char.ForeColor = &negro
	&Char.FontBold = 0

EndSub

// Procedimientos de formateo


====== PROPERTIES =======
Name -> HHrsNoIng2
Description -> Horas no ingresadas
IsMain -> False
IsPrompt -> False
AUTO_REFRESH -> NO
Apply:07135890-56fc-489b-b408-063722fa9f7d -> True
FolderType -> c88fffcd-b6f8-0000-8fec-00b5497e2117
FolderId -> 4
ObjectVisibility -> Public
IsDefault -> False
